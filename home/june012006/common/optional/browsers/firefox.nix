#FIXME: need to modularize thiso
#FIXME: figure out how to declare the following settings to mitigate stream stuttering on gusto from some sources
#   media.cache_readahead_limit = 150
#   media.cache_resume_threshold = 60

{ lib, config, ... }:
{
  programs.firefox = {
    enable = true;

    # Refer to https://mozilla.github.io/policy-templates or `about:policies#documentation` in firefox
    policies = {
      AppAutoUpdate = false; # Disable automatic application update
      BackgroundAppUpdate = false; # Disable automatic application update in the background, when the application is not running.
      DefaultDownloadDirectory = "${config.home.homeDirectory}/downloads";
      DisableBuiltinPDFViewer = false;
      DisableFirefoxStudies = true;
      DisableFirefoxAccounts = false; # Enable Firefox Sync
      DisablePocket = false; # enable once floccus is in use again
      DisableTelemetry = true;
      DontCheckDefaultBrowser = true;
      OfferToSaveLogins = false; # Managed by Proton
      EnableTrackingProtection = {
        Value = true;
        Locked = true;
        Cryptomining = true;
        Fingerprinting = true;
        EmailTracking = true;
        # Exceptions = ["https://example.com"]
      };
      ExtensionUpdate = true;

      # To copy extensions from an existing profile you can do something like this:
      # cat ~/.mozilla/firefox/fb8sickr.default/extensions.json | jq '.addons[] | [.defaultLocale.name, .id]'
      #
      # To add additional extensions, find it on addons.mozilla.org, find
      # the short ID in the url (like https://addons.mozilla.org/en-US/firefox/addon/!SHORT_ID!/)
      # Then, download the XPI by filling it in to the install_url template, unzip it,
      # run `jq .browser_specific_settings.gecko.id manifest.json` or
      # `jq .applications.gecko.id manifest.json` to get the UUID
      ExtensionSettings =
        (
          let
            extension = shortId: uuid: {
              name = uuid;
              value = {
                install_url = "https://addons.mozilla.org/en-US/firefox/downloads/latest/${shortId}/latest.xpi";
                installation_mode = "normal_installed";
              };
            };
          in
          lib.listToAttrs [
            #TODO Add more of these and test. not high priority though since mozilla sync will pull them in too
            # Development
            #(extension "user-agent-switcher" "{a6c4a591-f1b2-4f03-b3ff-767e5bedf4e7}") # failed

            # Privacy / Security
            (extension "noscript" "{73a6fe31-595d-460b-a920-fcc0f8843232}")
            (extension "ublock-origin" "uBlock0@raymondhill.net")
            (extension "ignore-cookies" "jid1-KKzOGWgsW3Ao4Q@jetpack") # failed # Ignore cookie setting pop-ups
            # (extension "proton-pass" "78272b6fa58f4a1abaac99321d503a20@proton.me")
            # (extension "proton-vpn-firefox-extension" "vpn@proton.ch") # failed
            (extension "privacy-badger17" "jid1-MnnxcxisBPnSXQ@jetpack")
            (extension "cookie-autodelete" "CookieAutoDelete@kennydo.com")
            (extension "Dashlane" "jetpack-extension@dashlane.com")

            # Layout / Themeing
            (extension "tree-style-tab" "treestyletab@piro.sakura.ne.jp")
            (extension "darkreader" "addon@darkreader.org")

            # Voice
            # (extension "domain-in-title" "{966515fa-4c81-4afe-9879-9bbaf8576390}")
            #(extension "rango" "rango@david-tejada")

            # Misc
            # (extension "s3download-statusbar" "{6913849f-c79f-4f3e-83e4-890d91ad6154}")
            # (extension "auto-tab-discard" "{c2c003ee-bd69-42a2-b0e9-6f34222cb046}")
            # (extension "reddit-enhancement-suite" "jid1-xUfzOsOFlzSOXg@jetpack")
          ]
        )
        // {
          # Disable built-in search engines
          "amazondotcom@search.mozilla.org" = {
            installation_mode = "blocked";
          };
          "bing@search.mozilla.org" = {
            installation_mode = "blocked";
          };
          "ebay@search.mozilla.org" = {
            installation_mode = "blocked";
          };
          "google@search.mozilla.org" = {
            installation_mode = "blocked";
          };
          #  "*" = {
          #  installation_mode = "blocked";
          #  blocked_install_message = "Install your extensions with Nix";
        };
    };
  };
}
